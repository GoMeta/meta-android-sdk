apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.gometa.metaverse.sdk.example.metaversesdkdemo"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "language"
    productFlavors {
        java {
            dimension "language"
            applicationIdSuffix ".java"
            versionNameSuffix "-java"
            buildConfigField("String", "LANGUAGE", "\"Java\"")
        }
        kotlin {
            dimension "language"
            applicationIdSuffix ".kotlin"
            versionNameSuffix "-kotlin"
            buildConfigField("String", "LANGUAGE", "\"Kotlin\"")
        }
    }
    sourceSets {
        java {
            java.srcDirs += 'src/java/java'
        }
        kotlin {
            java.srcDirs += 'src/kotlin/java'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "com.jakewharton.timber:timber:$versions.timber"
    implementation "io.gometa.metaverse:storyboard-sdk:$versions.storyboardSdk"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
